plugins {
    id 'com.github.johnrengelman.shadow'
}

configurations {
    implementation {
        canBeResolved = true
    }
}

repositories {
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        url = 'https://libraries.minecraft.net/'
    }
}

dependencies {
    implementation project(':points-common')
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    implementation 'me.lucko:commodore:1.10'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        expand 'project': project
    }
}

shadowJar {
    dependencies {
        include(dependency(":points-common"))
        include(dependency("ru.ruscalworld:storagelib"))
        include(dependency("net.kyori:.*"))
        include(dependency("me.lucko:commodore"))
        include(dependency("me.lucko:commodore-file"))
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate 'me.lucko.commodore', 'ru.ruscalworld.points.dependencies.commodore'
    relocate 'ru.ruscalworld.storagelib', 'ru.ruscalworld.points.dependencies.storagelib'
    relocate 'net.kyori.adventure', 'ru.ruscalworld.points.dependencies.adventure'
}

tasks.build.dependsOn tasks.shadowJar
